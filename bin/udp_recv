#!/usr/bin/perl -w
use strict;
use warnings;

my $VERSION = "1.000";

# modprobe fou
# ip link add name tun1 type ipip remote 127.0.0.1 local 10.252.3.1 ttl 225 encap fou encap-sport auto encap-dport 5555
# ip link set tun1 up
# ip addr add 10.253.4.1/24 dev tun1

# ip link del name tun1

use FindBin qw($Script);
our $Bin;

# If the program runs as /foobar/bin/program, find libraries in /foobar/lib
BEGIN {
    # Even on windows FindBin uses / in the reported path
    $Bin = $FindBin::Bin;
    $Bin =~ s{/+\z}{};
    $Bin =~
        ($^O eq "MSWin32" ?
         qr{^((?:[A-Z]:)?(?:/[a-zA-Z0-9_:.~ -]+)*)/[a-zA-Z0-9_.-]+/*\z} :
         qr{^((?:/[a-zA-Z0-9_:.-]+)*)/[a-zA-Z0-9_.-]+/*\z}) ||
         die "Could not parse bin directory '$Bin'";
    # Use untainted version of lib
    require lib;
    # Support a simple --blib option for pre-install testing
    "lib"->import(@ARGV && $ARGV[0] eq "--blib" ? shift && "$1/blib/lib" : "$1/lib");
}

$| = 1;
use Getopt::Long 2.24 qw(:config bundling require_order);
use IO::Socket::INET qw();
use IO::Socket qw(MSG_TRUNC MSG_CTRUNC
                  inet_ntoa unpack_sockaddr_in pack_sockaddr_in);
use Socket::MsgHdr;

use Net::Sssssh qw(IPPROTO_IP IP_PKTINFO IP_RECVTTL IP_RECVDSTADDR IP_TTL
                   ICMP_ECHO_REPLY ICMP_ECHO_REQUEST
                   fou_decode fou_encode_udp fou_encode_icmp string_from_value
                   parse_address);

my $port = 5555;
my $loops = -1;
die "Could not parse your command line. Try $0 -h\n" unless
    GetOptions("f|fou!"		=> \my $fou,
               "a|answer:s"	=> \my $answer,
               "p|port=o"	=> \$port,
               "l|loops=o"	=> \$loops,
               "v|verbose!"	=> \my $verbose,
               "version!"	=> \my $version,
               "U|unsafe!"	=> \my $unsafe,
               "h|help!"	=> \my $help);

if ($version) {
    ## no critic (UselessNoCritic RequireCheckedSyscalls)
    require Net::Sssssh::Package;
    print <<"EOF";
udp_recv $VERSION (Net::Sssssh $Net::Sssssh::Package::VERSION)
EOF
    exit 0;
}
if ($help) {
    require Config;
    $ENV{PATH} .= ":" unless $ENV{PATH} eq "";
    $ENV{PATH} = "$ENV{PATH}$Config::Config{'installscript'}";
    exit system("perldoc", "-F", $unsafe ? "-U" : (), "$Bin/$Script") ? 1 : 0;
    # make parser happy
    %Config::Config = ();
}

my $s = IO::Socket::INET->new(
    Proto	=> "udp",
    ReUse	=> 1,
    # LocalHost	=> "127.0.0.1",
    LocalPort	=> $port) || die "Could not create socket: $!";
setsockopt($s, IPPROTO_IP, IP_PKTINFO, 1) ||
    die "setsockopt(IPPROTO_IP, IP_PKTINFO): $^E";
setsockopt($s, IPPROTO_IP, IP_RECVTTL, 1) ||
    die "setsockopt(IPPROTO_IP, IP_RECVTTL): $^E";

if (defined $answer && $answer ne "") {
    my ($aport, $aaddr) = unpack_sockaddr_in(
        my $tmp = parse_address($answer, "Answer", to => "UDP4")) or
            die "Assertion: Cannot unpack packed answer '$answer'";
    printf("Answers to %s:%d\n", inet_ntoa($aaddr), $aport) if $verbose;
    $answer = $tmp;
}
# my $buffer = "";
my $msghdr = Socket::MsgHdr->new();
print "=" x 25 . "\n" if $verbose;
while ($loops--) {
    $msghdr->controllen(256);
    $msghdr->namelen(256);
    $msghdr->buflen(2**16);
    $s->recvmsg($msghdr) || die "Could not recvmsg: $!";

    # Socket::MsgHdr 0.05 does in fact not set flags after recvmsg
    die "Yikes! Buffer was truncated\n" if
        $msghdr->flags & MSG_TRUNC;
    die "Yikes! Ancillary data was truncated\n" if
        $msghdr->flags & MSG_CTRUNC;

    my ($local, $msg_dst, $ttl);
    my @cmsg = $msghdr->cmsghdr();
    for my $i (0 .. $#cmsg / 3) {
        $i *= 3;
        my ($level, $type, $data) = @cmsg[$i..$i+2];
        $level == IPPROTO_IP || next;
        if (defined IP_RECVDSTADDR && $type == IP_RECVDSTADDR) {
            # FreeBSD
            length $data == 4 || die "Unexpected IP_RECVDSTADDR length";
            $local = inet_ntoa($data);
            $msg_dst = $local;
        } elsif (defined IP_PKTINFO && $type == IP_PKTINFO) {
            # Linux
            length $data == 12 || die "Unexpected IP_PKTINFO length";
            my ($interface, $dst, $to) = unpack("Ia4a4", $data);
            $local = inet_ntoa($to);
            $msg_dst = inet_ntoa($dst);
        } elsif ($type == IP_TTL) {
            length $data == 4 || die "Unexpected IP_TTL length";
            $ttl = unpack("I", $data);
            $cmsg[$i+2] = pack("I", $ttl-1);
        }
    }
    $local // die "Assertion: No local addr";
    $ttl   // die "Assertion: No TTL";

    my $sender = $msghdr->name;
    my ($peer_port, $peer) = unpack_sockaddr_in($sender);
    $peer = inet_ntoa($peer);

    my $buffer = $msghdr->buf;
    my $props;
    if ($fou) {
        $props = fou_decode($buffer, $verbose && \*STDOUT, 1);
        $buffer = $props->{data} // redo;
    }
    print "Sender $peer:$peer_port, Receiver $local [$msg_dst]\n" if $verbose;
    print "Received: ", length($buffer), " bytes (TTL $ttl) ", string_from_value($buffer), "\n" if $verbose;

    if (defined $answer) {
        if ($ttl > 1) {
            $msghdr->cmsghdr(@cmsg);
            $msghdr->name($answer) if $answer;
            unless ($fou && $props->{proto} eq "icmp") {
                $buffer = time() . ": " . $buffer if $buffer !~ s/^(\d+)/$1+1/ea;
            }
            if ($fou) {
                if ($props->{ttl} <= 1) {
                    print "Sent nothing (received TTL=$props->{ttl})\n" if $verbose;
                    print "=" x 25 . "\n" if $verbose;
                    exit;
                }
                if ($props->{proto} eq "udp") {
                    $buffer = fou_encode_udp(
                        pack_sockaddr_in($props->{dprt}, $props->{dst}),
                        pack_sockaddr_in($props->{sprt}, $props->{src}),
                        $buffer,
                        $verbose && \*STDOUT, $props->{ttl}-1);
                } elsif ($props->{proto} eq "icmp") {
                    $props->{type} == ICMP_ECHO_REQUEST ||
                        die "No answer for ICMP type $props->{type}";
                    $buffer = fou_encode_icmp(
                        inet_ntoa($props->{dst}),
                        inet_ntoa($props->{src}),
                        ICMP_ECHO_REPLY, 0,
                        $props->{header_icmp},
                        $buffer,
                        $verbose && \*STDOUT);
                } else {
                    die "Protocol '$props->{proto}' not implemented (yet)";
                }
                # fou_decode($buffer, \*STDOUT) if $verbose;
            }
            $msghdr->buf($buffer);
            $s->sendmsg($msghdr) || die "Could not send: $^E";
            printf("Sent: %d bytes (TTL %d) %s\n",
                   length($buffer), $ttl-1, string_from_value($buffer)) if $verbose;
        } else {
            print "Sent nothing (received TTL=$ttl)\n" if $verbose;
        }
    }
    print "=" x 25 . "\n" if $verbose;
}
